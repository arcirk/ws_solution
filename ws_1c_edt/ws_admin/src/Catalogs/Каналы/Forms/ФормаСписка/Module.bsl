
&НаКлиенте
Асинх Процедура ВыгрузитьСтруктуру(Команда)
	
	Результат = СправочникВJSON();
	
	Текст = Новый ТекстовыйДокумент();
	Текст.УстановитьТекст(Результат);
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Диалог.Фильтр = "json|*.json";
	
	мРезультат = Ждать Диалог.ВыбратьАсинх();	
	
	Если мРезультат <> Неопределено И мРезультат.Количество() > 0 Тогда
		Текст.ЗаписатьАсинх(Диалог.ПолноеИмяФайла, "CESU-8");	
	КонецЕсли;
	
	
КонецПроцедуры

&НаСервере
Функция СправочникВJSON()
	
	root = Справочники.Каналы.НайтиПоНаименованию("root");
	
	Результат = Новый Структура("name,uuid,child", "root", XMLСтрока(root), Новый Массив());
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Каналы.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Каналы КАК Каналы
	|ГДЕ
	|	НЕ Каналы.Ссылка.ПометкаУдаления
	|ИТОГИ
	|ПО
	|	Ссылка ИЕРАРХИЯ КАК Ссылка";
	
	РезультатЗапроса = Запрос.Выполнить(); 
	РезультатДерево = РезультатЗапроса.Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией).Строки[0];
	
	ЗаполнитьДеревоСтруктур(РезультатДерево, Результат);
	
	Запись = Новый ЗаписьJSON();
	
	Запись.УстановитьСтроку();
	ЗаписатьJSON(Запись, Результат);
	Возврат Запись.Закрыть();	
	
КонецФункции

Процедура ЗаполнитьДеревоСтруктур(Источник, Результат) 
	
	мДочерние = Новый Массив;

	Для Каждого ТекСтрока Из Источник.Строки Цикл
		
		Если ТекСтрока.Родитель <> Неопределено Тогда
			Если ТекСтрока.Ссылка = ТекСтрока.Родитель.Ссылка Тогда
				Продолжить;
			КонецЕсли;              
		КонецЕсли;
		
		Элемент = Новый Структура("name,uuid,child",
		ТекСтрока.Ссылка.Наименование, XMLСтрока(ТекСтрока.Ссылка), Новый Массив); 
		
        ЗаполнитьДеревоСтруктур(ТекСтрока, Элемент);  
		
		мДочерние.Добавить(Элемент);
		
	КонецЦикла;
	
	Результат.Вставить("child", мДочерние)
	
	//	
КонецПроцедуры


&НаКлиенте
Асинх Процедура ЗагрузитьСтруктуру(Команда)
#Если НЕ ВебКлиент Тогда
	Ответ = Ждать ВопросАсинх("Справочник будет очищен! При выгрузки справочника на сервер идентификаторы на сервере буду заменены на текщие. Продолжить?",
	РежимДиалогаВопрос.ДаНет);
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Фильтр = "json|*.json";
	Результат = Ждать Диалог.ВыбратьАсинх();
	Если Результат.Количество() > 0  Тогда
		ДвоичныеДанные = Новый ДвоичныеДанные(Результат[0]);
		Адрес = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
		ЗагрузитьСправочник(Адрес);		
	КонецЕсли;	
#КонецЕсли	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьСправочник(Адрес)
	
	Чтение = Новый ЧтениеJSON();
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(Адрес);
	ИмяФайла = ПолучитьИмяВременногоФайла();
	ДвоичныеДанные.Записать(ИмяФайла);
	Чтение.ОткрытьФайл(ИмяФайла);
	Результат = ПрочитатьJSON(Чтение);
	ОчиститьСправочник();
	ТекОбъект = Справочники.Каналы.СоздатьЭлемент();
	ТекОбъект.Наименование = Результат.name;
	ТекОбъект.Записать();
	ЗагрузитьСправочникИзСтруктуры(Результат.child, ТекОбъект.Ссылка)
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьСправочник()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Каналы.Ссылка
		|ИЗ
		|	Справочник.Каналы КАК Каналы";
	
	Результат = Запрос.Выполнить().Выгрузить();

	Для Каждого ТекСсылка Из Результат Цикл
		Объект = ТекСсылка.Ссылка.ПолучитьОбъект();
		Объект.Удалить();	
	КонецЦикла;
	
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьСправочникИзСтруктуры(мЭлементы, Родитель)
	
	Для Каждого ТекЭлемент Из мЭлементы Цикл
		ТекОбъект = Справочники.Каналы.СоздатьЭлемент();
		ТекОбъект.Наименование = ТекЭлемент.name;
		ТекОбъект.Родитель = Родитель;
		ТекОбъект.Записать();
		ЗагрузитьСправочникИзСтруктуры(ТекЭлемент.child, ТекОбъект.Ссылка)
	КонецЦикла;
	
КонецПроцедуры


