&НаКлиенте
Перем КэшСообщения;

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ПолучательИмя", ПолучательИмя);
	Параметры.Свойство("ИдентфикаторСессииПолучателя", ИдентфикаторСессииПолучателя);
	Параметры.Свойство("ПолучательИдентификатор", ПолучательИдентификатор);
	
	Макет = Обработки.WebSocketService.ПолучитьМакет("ПолеЧата");
	Заголовок = "Чат - " + ПолучательИмя;
	ПолеHTML = Макет.ПолучитьТекст();
	//Элементы.ПолеHTML.УстановитьТекст(Макет.ПолучитьТекст());	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьСообщение(Команда)
	Если ВебСокетКлиент.Запущен() Тогда
		ВебСокетКлиент.Сообщить(ТекстСообщения, ПолучательИдентификатор, Строка(УникальныйИдентификатор));
	КонецЕсли;  
	ТекстСообщения = "";
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ВебСокетКлиент = Неопределено Тогда
		Отказ = Истина
	Иначе
		Если НЕ ВебСокетКлиент.Запущен() Тогда
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;   
	
	Если Отказ ТОгда
		#Если НЕ ВебКлиент Тогда
		ПредупреждениеАсинх("Клинет не подключен!");
		#КонецЕсли
	Иначе
		Если ВебСокетКлиент <> Неопределено И ВебСокетКлиент.Запущен() Тогда
			ВебСокетКлиент.ПолучитьСообщения(ПолучательИдентификатор, ДобавитьМесяц(ТекущаяДата(), -1), ТекущаяДата(), 20, Строка(УникальныйИдентификатор));		
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВнешнееСобытие(Источник, Событие, Данные)
	
	Текст = WebSocketКлиент.Base64ВСтроку(Данные);

	СтруктураСообщения = WebSocketСервер.ПолучитьСтруктуруСообщения(Текст);
	
	Если СтруктураСообщения.result = "error" Тогда
		Сообщить(СтруктураСообщения.message, СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;
	
	Если СтруктураСообщения.Свойство("uuid_form") Тогда
		Если СтруктураСообщения.uuid_channel <> ВебСокетКлиент.ИдентификаторПользователя И  СокрЛП(СтруктураСообщения.uuid_form) <> "" И НЕ Новый УникальныйИдентификатор(СтруктураСообщения.uuid_form) = УникальныйИдентификатор Тогда
			Возврат;  
		Иначе
			Если СокрЛП(СтруктураСообщения.uuid_form) = "" Тогда
				Сообщить(СтруктураСообщения.message); 
				Возврат;
			КонецЕсли;
		КонецЕсли;
	Иначе
		Возврат;
	КонецЕсли; 

	Если СтруктураСообщения.command <> "message" Тогда 
		Если СтруктураСообщения.command = "get_messages" Тогда
			ЗагрузитьСообщения(СтруктураСообщения.message);
			ВебСокетКлиент.ПолучитьИнформациюОПользователе(ПолучательИдентификатор, Строка(УникальныйИдентификатор));	
		ИначеЕсли СтруктураСообщения.command = "get_user_info" Тогда 
			ИнформацияОПользователе = РаботаСJSON.ПрочитатьОтветСервера(СтруктураСообщения.message);
			Если ТипЗнч(ИнформацияОПользователе) = Тип("Структура") Тогда
				Если ИнформацияОПользователе.Свойство("performance") ТОгда
					ЭтаФорма.Заголовок = "Чат: " + ИнформацияОПользователе.performance + "(" + ?(ИнформацияОПользователе.active, "В сети", "Не в сети" + ")");	
				Иначе
					ЭтаФорма.Заголовок = "Чат: " + ИнформацияОПользователе.name + "(" + ?(ИнформацияОПользователе.active, "В сети", "Не в сети") + ")";
				КонецЕсли;
				АктивностьПолучателя = ИнформацияОПользователе.active;
			КонецЕсли;
		ИначеЕсли СтруктураСообщения.command = "client_leave" Тогда
			ВебСокетКлиент.ПолучитьИнформациюОПользователе(ПолучательИдентификатор, Строка(УникальныйИдентификатор));
		КонецЕсли;
	Иначе 
		ОбработатьСообщениеСервера(СтруктураСообщения);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Асинх Процедура ЗагрузитьСообщения(ОтветСервера)
	
	мСообщения = РаботаСJSON.ПрочитатьОтветСервера(ОтветСервера);
	
	Результат = ОчиститьСписок();
	Если НЕ Результат Тогда
		КэшСообщения = мСообщения;
		ПодключитьОбработчикОжидания("ОжидатьИнициализацииJavaScript", 1);
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(мСообщения) = Тип("Массив") Тогда
		Для Каждого Элемент Из мСообщения Цикл
		     СтруктураСообщения = РаботаСJSON.ПрочитатьОтветСервера(WebSocketКлиент.Base64ВСтроку(Элемент.message)); 
			 ОбработатьСообщениеСервера(СтруктураСообщения,  Дата(1970,1,1,1,0,0) + Элемент.date + ВебСокетКлиент.ПолучитьСмещениеВременнойЗоны());
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОжидатьИнициализацииJavaScript() Экспорт
		
	Результат = ОчиститьСписок();
	Если Результат Тогда
		ОтключитьОбработчикОжидания("ОжидатьИнициализацииJavaScript");
		Если ТипЗнч(КэшСообщения) = Тип("Массив") Тогда
			Для Каждого Элемент Из КэшСообщения Цикл
			     СтруктураСообщения = РаботаСJSON.ПрочитатьОтветСервера(WebSocketКлиент.Base64ВСтроку(Элемент.message)); 
				 ОбработатьСообщениеСервера(СтруктураСообщения,  Дата(1970,1,1,1,0,0) + Элемент.date + ВебСокетКлиент.ПолучитьСмещениеВременнойЗоны());
			КонецЦикла;
			КэшСообщения = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ОчиститьСписок() Экспорт  
	
	ДокументПервогоБраузера = Элементы.ПолеHTML.Документ;
    ОкноДокумента     = ДокументПервогоБраузера.parentWindow; 
    Если ОкноДокумента = Неопределено Тогда
        ОкноДокумента = ДокументПервогоБраузера.defaultView;
	КонецЕсли;
	
	Попытка
		ОкноДокумента.clear_list();
	Исключение
		Возврат Ложь;
	КонецПопытки;
	Возврат Истина;
КонецФункции

&НаКлиенте
Асинх Процедура ОбработатьСообщениеСервера(СтруктураСообщения, ДатаСообщения = Неопределено)
	
	Если СтруктураСообщения.result = "error" Тогда
		Сообщить("Ошибка: " + СтруктураСообщения.message, СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;
	
	ДокументПервогоБраузера = Элементы.ПолеHTML.Документ;
    ОкноДокумента     = ДокументПервогоБраузера.parentWindow; 
    Если ОкноДокумента = Неопределено Тогда
        ОкноДокумента = ДокументПервогоБраузера.defaultView; 
	КонецЕсли;
	
	Если ДатаСообщения = Неопределено Тогда
		ДатаСообщения = ТекущаяДата();
	КонецЕсли;
	
	Попытка
		ОкноДокумента.push_message(Формат(ДатаСообщения, "ДЛФ=DT") + " " + СтруктураСообщения.name + ":" + СтруктураСообщения.message);
	Исключение
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСообщения(Команда)
	Попытка
		ДокументПервогоБраузера = Элементы.ПолеHTML.Документ;
	    ОкноДокумента     = ДокументПервогоБраузера.parentWindow; 
	    Если ОкноДокумента = Неопределено Тогда
	        ОкноДокумента = ДокументПервогоБраузера.defaultView;
	    КонецЕсли;
	    ОкноДокумента.clear_list();
		Если ВебСокетКлиент <> Неопределено И ВебСокетКлиент.Запущен() Тогда
			ВебСокетКлиент.ПолучитьСообщения(ПолучательИдентификатор, ДобавитьМесяц(ТекущаяДата(), -1), ТекущаяДата(), 20, Строка(УникальныйИдентификатор));		
		КонецЕсли;
	Исключение
		Сообщить(ОписаниеОшибки());
	КонецПопытки;
КонецПроцедуры


